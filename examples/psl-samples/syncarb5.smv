--syncarb.smv:

MODULE arbiter-element(above,below,init-token)

VAR
  Persistent : boolean;
  Token : boolean;
  Request : boolean;

ASSIGN
  init(Token) := init-token;
  next(Token) := token-in;
  init(Persistent) := 0;
  next(Persistent) := Request & (Persistent | Token);

DEFINE
  above.token-in := Token;
  override-out := above.override-out | (Persistent & Token);
  grant-out := !Request & below.grant-out;
  ack-out := Request & (Persistent & Token | below.grant-out);

SPEC
  AG ((ack-out -> Request) & AF (!Request | ack-out))

PSLSPEC
  AG ((ack-out -> Request) & AF (!Request | ack-out));

MODULE main

VAR
  e[5] : arbiter-element(self,e[4],0);
  e[4] : arbiter-element(e[5],e[3],0);
  e[3] : arbiter-element(e[4],e[2],0);
  e[2] : arbiter-element(e[3],e[1],0);
  e[1] : arbiter-element(e[2],self,1);

DEFINE
  grant-in := 1;
  e[1].token-in := token-in;
  override-out := 0;
  grant-out := grant-in & !e[1].override-out;

--SPEC
--  AG (e[5].Request -> ABF 0..4 (!e[5].Request | e[5].ack-out))

--SPEC
--  AG (!e[1].Request & !e[2].Request & !e[3].Request & !e[4].Request & e[5].Request -> e[5].ack-out)

SPEC
  AG (
      !(e[1].ack-out & e[2].ack-out)

      & !(e[1].ack-out & e[3].ack-out)
      & !(e[2].ack-out & e[3].ack-out)

      & !(e[1].ack-out & e[4].ack-out)
      & !(e[2].ack-out & e[4].ack-out)
      & !(e[3].ack-out & e[4].ack-out)

      & !(e[1].ack-out & e[5].ack-out)
      & !(e[2].ack-out & e[5].ack-out)
      & !(e[3].ack-out & e[5].ack-out)
      & !(e[4].ack-out & e[5].ack-out)
     )


PSLSPEC
  AG (
      !(e[1].ack-out & e[2].ack-out)

      & !(e[1].ack-out & e[3].ack-out)
      & !(e[2].ack-out & e[3].ack-out)

      & !(e[1].ack-out & e[4].ack-out)
      & !(e[2].ack-out & e[4].ack-out)
      & !(e[3].ack-out & e[4].ack-out)

      & !(e[1].ack-out & e[5].ack-out)
      & !(e[2].ack-out & e[5].ack-out)
      & !(e[3].ack-out & e[5].ack-out)
      & !(e[4].ack-out & e[5].ack-out)
     );


PSLSPEC
      AG (
                           ! (e[1].ack-out & e[2].ack-out)
      & forall i in {1:2}: ! (e[i].ack-out & e[3].ack-out)
      & forall i in {1:3}: ! (e[i].ack-out & e[4].ack-out)
      & forall i in {1:4}: ! (e[i].ack-out & e[5].ack-out)
      ) ;

PSLSPEC
      always (
                           ! (e[1].ack-out & e[2].ack-out)
      & forall i in {1:2}: ! (e[i].ack-out & e[3].ack-out)
      & forall i in {1:3}: ! (e[i].ack-out & e[4].ack-out)
      & forall i in {1:4}: ! (e[i].ack-out & e[5].ack-out)
      ) ;


PSLSPEC
                           never (e[1].ack-out & e[2].ack-out)
      & forall i in {1:2}: never (e[i].ack-out & e[3].ack-out)
      & forall i in {1:3}: never (e[i].ack-out & e[4].ack-out)
      & forall i in {1:4}: never (e[i].ack-out & e[5].ack-out);

PSLSPEC
      never forall i in {1:2}: (e[i].ack-out & e[3].ack-out);

PSLSPEC
      never forall i in {1:3}: (e[i].ack-out & e[4].ack-out);

PSLSPEC
      never forall i in {1:4}: (e[i].ack-out & e[5].ack-out);
